REM ** FILL DRAWING AREA WITH CHARACTERS
I=8
CHAR ,(0,0,0,0,0)
FOR CY=0 TO 8
  FOR CX=0 TO 19
    CHAR I,
    CELL CX,CY+3
    I=I+1
  NEXT CX
NEXT CY

REM ** SOME TEXT
TEXT 0,0, "     DRAWING WITH TILES?"
TEXT 0,15,"                     NO PROBLEM!"

REM ** ANIMATE!
I=0
RY=0
ON RASTER GOSUB RASTFX
DO
  FILL $8080,2880,0

  DIV=40+SIN(I*0.02)*20
  BP=0
  GOSUB DRAWSINE

  DIV=30+SIN(I*0.03)*10
  BP=1
  GOSUB DRAWSINE

  I=I+1
  RY=INT(48-SIN(I*0.04)*48)
  WAIT VBL
LOOP

RASTFX:
REM ** SCROLL TOP AND BOTTOM TEXT LINE
IF RASTER<8 OR RASTER>=120 THEN DISPLAY 0,I,0 ELSE DISPLAY 0,0,0
REM ** PALETTE FX
IF RASTER=0 OR RASTER=RY+33 THEN PALETTE 0,%000001,%110000,%001100,%110100
IF RASTER=RY THEN PALETTE 0,%000010,%001111,%110011,%111100
RETURN

DRAWSINE:
FOR X=0 TO 159
  Y=36+SIN(X*PI/DIV)*35
  REM ** GET CHARACTER
  CX=INT(X/8)
  CY=INT(Y/8)
  CI=CY*20+CX
  REM ** GET BIT
  BI=2^(7-(X MOD 8))
  REM ** WRITE IN CHARACTER RAM
  A=$8080+CI*16+(Y MOD 8)+BP*8
  POKE A,PEEK(A) OR BI
NEXT X
RETURN
